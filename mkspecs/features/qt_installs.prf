#
#  W A R N I N G
#  -------------
#
# This file is not part of the Qt API.  It exists purely as an
# implementation detail.  It may change from version to version
# without notice, or even be removed.
#
# We mean it.
#

#library
!qt_no_install_library {
    win32 {
        dlltarget.path = $$NIX_OUTPUT_BIN/bin
       INSTALLS += dlltarget
    }
    target.path = $$NIX_OUTPUT_OUT/lib
    !static: target.CONFIG = no_dll
    INSTALLS += target
}

#headers
qt_install_headers {
    class_headers.files = $$SYNCQT.HEADER_CLASSES
    class_headers.path = $$NIX_OUTPUT_DEV/include/$$MODULE_INCNAME
    INSTALLS += class_headers

    targ_headers.files = $$SYNCQT.HEADER_FILES
    targ_headers.path = $$NIX_OUTPUT_DEV/include/$$MODULE_INCNAME
    INSTALLS += targ_headers

    private_headers.files = $$SYNCQT.PRIVATE_HEADER_FILES
    private_headers.path = $$NIX_OUTPUT_DEV/include/$$MODULE_INCNAME/$$VERSION/$$MODULE_INCNAME/private
    INSTALLS += private_headers

    qpa_headers.files = $$SYNCQT.QPA_HEADER_FILES
    qpa_headers.path = $$NIX_OUTPUT_DEV/include/$$MODULE_INCNAME/$$VERSION/$$MODULE_INCNAME/qpa
    INSTALLS += qpa_headers
}

#module
qt_install_module {
    !isEmpty(MODULE_PRI) {
        pritarget.path = $$NIX_OUTPUT_DEV/mkspecs/modules
        pritarget.files = $$MODULE_PRI
        INSTALLS += pritarget
    } else: isEmpty(MODULE_PRIVATE_PRI) {
        warning("Project $$basename(_PRO_FILE_) is a module, but has not defined MODULE_PRI, which is required for Qt to expose the module to other projects.")
    }
    !isEmpty(MODULE_PRIVATE_PRI) {
        privpritarget.path = $$NIX_OUTPUT_DEV/mkspecs/modules
        privpritarget.files = $$MODULE_PRIVATE_PRI
        INSTALLS += privpritarget
    }
}
